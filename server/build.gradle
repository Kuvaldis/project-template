ext {
    global = v
    v = [
            commonsDaemon : '1.0.15',
            liquibaseSlf4j: '1.2.1',
            jetty         : '9.2.1.v20140609',
            springSecurity: '3.2.4.RELEASE'
    ]
    v.putAll global
}

dependencies {
    compile project(':core')
    compile project(':rest-api')
    compile group: 'commons-daemon', name: 'commons-daemon', version: v.commonsDaemon
    compile group: 'org.slf4j', name: 'jul-to-slf4j', version: v.slf4j
    compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: v.slf4j

    compile(group: 'org.eclipse.jetty', name: 'jetty-server', version: v.jetty) {
        exclude group: 'javax.servlet'
    }
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: v.jetty
    compile group: 'org.springframework', name: 'spring-web', version: v.spring

    compile group: 'org.springframework.security', name: 'spring-security-core', version: v.springSecurity
    compile(group: 'org.springframework.security', name: 'spring-security-web', version: v.springSecurity) {
        exclude group: 'org.springframework'
    }
    compile group: 'org.springframework.security', name: 'spring-security-config', version: v.springSecurity

    runtime group: 'com.mattbertolini', name: 'liquibase-slf4j', version: v.liquibaseSlf4j
}


task libs(type: Sync) {
    from configurations.runtime
    into "$buildDir/lib"
}

task copyConfig(type: Sync) {
    from "$buildDir/../../config/server-config.groovy",
            "$buildDir/../../config/server-profiles-config.groovy"
    into "$buildDir/config"
}

jar {
    baseName = 'server'
    destinationDir = file("$buildDir")
    manifest {
        attributes(
                'Main-Class': 'kuvaldis.server.Start',
                'Class-Path': configurations.runtime.collect { "lib/${it.name}" }.join(" ")
        )
    }
}

task zip(type: Zip) {
    baseName 'server'
    from("$buildDir") {
        include 'config/**'
        include 'lib/**'
        include 'server-*.jar'
    }
}

task build(overwrite: true) {
}
build.dependsOn 'jar'
build.dependsOn 'libs'
build.dependsOn 'copyConfig'
build.dependsOn 'zip'